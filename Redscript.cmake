# MOD_SLUG
# MOD_TOOLS_DIR
# MOD_GAME_DIR
# MOD_REDSCRIPT_DIR
# CYBERPUNK_2077_REDSCRIPT_BACKUP

include(Header)

# project(redscript)

set(REDSCRIPT_PREREQ_FILE "${PROJECT_BINARY_DIR}/dependencies.redscripts")
set(REDSCRIPT_LAST_LINT "${PROJECT_BINARY_DIR}/redscript.lint")
set(REDSCRIPT_PACKED_FILE "${MOD_GAME_DIR}/r6/scripts/${MOD_SLUG}/${MOD_SLUG}.packed.reds")
set(REDSCRIPT_MODULE_FILE "${MOD_GAME_DIR}/r6/scripts/${MOD_SLUG}/${MOD_SLUG}.module.reds")
set(REDSCRIPT_MODULE_TEMP "${PROJECT_BINARY_DIR}/${MOD_SLUG}.module.reds")
set(REDSCRIPT_PRECOMPILED "${PROJECT_SOURCE_DIR}/final.redscripts")
set(REDSCRIPT_MODULE_IN_FILE "${MOD_REDSCRIPT_DIR}/Module.reds.in")
set(REDSCRIPT_PRECOMPILE_DIR "${PROJECT_BINARY_DIR}/redscript/precompile")

if(EXISTS "${REDSCRIPT_MODULE_IN_FILE}")
  configure_file("${REDSCRIPT_MODULE_IN_FILE}" "${REDSCRIPT_MODULE_TEMP}" @ONLY)
  add_custom_command(
    OUTPUT ${REDSCRIPT_MODULE_FILE}
    DEPENDS ${REDSCRIPT_MODULE_TEMP}
    COMMAND ${CMAKE_COMMAND} -E copy_if_different "${REDSCRIPT_MODULE_TEMP}" "${REDSCRIPT_MODULE_FILE}"
    COMMENT "Creating redscript module file"
    USES_TERMINAL)
  add_custom_target(${MOD_SLUG}_redscript_module ${REDSCRIPT_MODULE_FILE})
  set_target_properties(${MOD_SLUG}_redscript_module PROPERTIES FOLDER "${FOLDER_PREFIX}Redscript")
  add_dependencies(${MOD_SLUG} ${MOD_SLUG}_redscript_module)
endif()

foreach(REDSCRIPT_DEPENDENCY_FILE ${REDSCRIPT_DEPENDENCIES_SOURCE_FILES})
file(RELATIVE_PATH REL_PATH ${PROJECT_SOURCE_DIR} ${REDSCRIPT_DEPENDENCY_FILE})
  configure_file("${REDSCRIPT_DEPENDENCY_FILE}" "${REDSCRIPT_PRECOMPILE_DIR}/${REL_PATH}")
endforeach()

add_custom_command(
  OUTPUT ${REDSCRIPT_PREREQ_FILE}
  DEPENDS ${REDSCRIPT_DEPENDENCIES_SOURCE_FILES}
  COMMAND ${REDSCRIPT_CLI_EXE}
  ARGS compile -s "${REDSCRIPT_PRECOMPILE_DIR}" -b "${CYBERPUNK_2077_REDSCRIPT_BACKUP}" -o "${REDSCRIPT_PREREQ_FILE}"
  COMMENT "Precompiling redscript dependencies")

file(GLOB_RECURSE REDSCRIPT_SOURCE_FILES ${MOD_REDSCRIPT_DIR}/*.reds LIST_DIRECTORIES false)

if(NOT PROJECT_IS_TOP_LEVEL)
  list(APPEND REDSCRIPT_DEPENDENCIES_SOURCE_FILES ${REDSCRIPT_SOURCE_FILES})
  set(REDSCRIPT_DEPENDENCIES_SOURCE_FILES ${REDSCRIPT_DEPENDENCIES_SOURCE_FILES} CACHE INTERNAL "Files used in compiling redscript dependencies")
endif()

add_custom_command(
  OUTPUT ${REDSCRIPT_LAST_LINT}
  DEPENDS ${REDSCRIPT_PREREQ_FILE} ${REDSCRIPT_SOURCE_FILES}
  COMMAND ${REDSCRIPT_CLI_EXE} lint -s ${MOD_REDSCRIPT_DIR} -b ${REDSCRIPT_PREREQ_FILE} && echo "1" > ${REDSCRIPT_LAST_LINT}
  COMMENT "Linting redscript against pre-compiled prereqs"
  USES_TERMINAL)

add_custom_target(${MOD_SLUG}_redscript_lint
  DEPENDS ${REDSCRIPT_PREREQ_FILE}
  COMMAND ${REDSCRIPT_CLI_EXE} lint -s ${MOD_REDSCRIPT_DIR} -b ${REDSCRIPT_PREREQ_FILE} && echo "1" > ${REDSCRIPT_LAST_LINT}
  USES_TERMINAL
  )
set_target_properties(${MOD_SLUG}_redscript_lint PROPERTIES FOLDER "${FOLDER_PREFIX}Redscript")

add_custom_command(
  OUTPUT ${REDSCRIPT_PACKED_FILE}
  BYPRODUCTS ${REDSCRIPT_PACKED_FILE}
  DEPENDS ${MOD_SLUG}_redscript_lint ${REDSCRIPT_SOURCE_FILES}
  COMMAND ${CMAKE_COMMAND} -D COMMENT_SLUG="//" -D GLOB_EXT="reds" -D HEADER_FILE="${CMAKE_CURRENT_LIST_DIR}/Header.txt" -D PACKED_FILE=${REDSCRIPT_PACKED_FILE} -D SEARCH_FOLDER=${MOD_REDSCRIPT_DIR} -P ${CMAKE_CURRENT_LIST_DIR}/PackFiles.cmake
  COMMENT "Packing redscript files into one")


add_custom_target(${MOD_SLUG}_redscript 
  DEPENDS ${REDSCRIPT_LAST_LINT} ${REDSCRIPT_PACKED_FILE}
  SOURCES ${REDSCRIPT_SOURCE_FILES}
  )
set_target_properties(${MOD_SLUG}_redscript PROPERTIES FOLDER "${FOLDER_PREFIX}Redscript")
add_dependencies(${MOD_SLUG} ${MOD_SLUG}_redscript)