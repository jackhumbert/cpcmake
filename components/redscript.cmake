
set(REDSCRIPT_MODULE_IN_FILENAME "Module.reds.in")

set(REDSCRIPT_PREREQ_FILE "${PROJECT_BINARY_DIR}/dependencies.redscripts")
set(REDSCRIPT_LAST_LINT "${PROJECT_BINARY_DIR}/redscript.lint")
set(REDSCRIPT_PRECOMPILE_DIR "${PROJECT_BINARY_DIR}/redscript/precompile")

set(MOD_GAME_DIR_REDSCRIPT_DIR "${MOD_GAME_DIR}/r6/scripts")

set(MOD_GAME_DIR_REDSCRIPT_PACKED_FILE "${MOD_GAME_DIR_REDSCRIPT_DIR}/${MOD_SLUG}/${MOD_SLUG}.packed.reds")
set(MOD_GAME_DIR_REDSCRIPT_MODULE_FILE "${MOD_GAME_DIR_REDSCRIPT_DIR}/${MOD_SLUG}/${MOD_SLUG}.module.reds")

set(MOD_GAME_DIR_RED4EXT_PACKED_FILE "${MOD_GAME_DIR_RED4EXT_DIR}/${MOD_SLUG}/packed.reds")
set(MOD_GAME_DIR_RED4EXT_MODULE_FILE "${MOD_GAME_DIR_RED4EXT_DIR}/${MOD_SLUG}/module.reds")

set(REDSCRIPT_PACK_INTO_RED4EXT OFF)

#[[Configures redscript at the location passed:
configure_redscipt(src/redscript)
Sets `MOD_REDSCRIPT_DIR` and uses the following variables:
* MOD_SLUG
* MOD_TOOLS_DIR
* MOD_GAME_DIR
* CYBERPUNK_2077_REDSCRIPT_BACKUP
]]
macro(configure_redscript REDSCRIPT_DIR)
  set(REDSCRIPT_DIR_RAW ${REDSCRIPT_DIR})

  if("${REDSCRIPT_DIR_RAW}" STREQUAL "")
    set(REDSCRIPT_DIR_RAW ${CMAKE_CURRENT_SOURCE_DIR})
  endif()

  cmake_path(IS_RELATIVE REDSCRIPT_DIR_RAW IS_REDSCRIPT_DIR_RELATIVE)

  if(${IS_REDSCRIPT_DIR_RELATIVE})
    set(MOD_REDSCRIPT_DIR ${MOD_SOURCE_DIR}/${REDSCRIPT_DIR_RAW})
  else()
    set(MOD_REDSCRIPT_DIR ${REDSCRIPT_DIR_RAW})
  endif()

  if(NOT ${REDSCRIPT_PACK_INTO_RED4EXT})
    list(APPEND ${MOD_PREFIX}_UNINSTALL_LOCATIONS "${MOD_GAME_DIR_REDSCRIPT_DIR}")
    set(MOD_GAME_DIR_PACKED_FILE ${MOD_GAME_DIR_REDSCRIPT_PACKED_FILE})
    set(MOD_GAME_DIR_MODULE_FILE ${MOD_GAME_DIR_REDSCRIPT_MODULE_FILE})
  else()
    set(MOD_GAME_DIR_PACKED_FILE ${MOD_GAME_DIR_RED4EXT_PACKED_FILE})
    set(MOD_GAME_DIR_MODULE_FILE ${MOD_GAME_DIR_RED4EXT_MODULE_FILE})
  endif()

  # file(RELATIVE_PATH REDSCRIPT_DIR_RELATIVE "${MOD_SOURCE_DIR}" "${MOD_REDSCRIPT_DIR}")
  message(STATUS "Configuring redscript files in ${MOD_REDSCRIPT_DIR}")

  include(Header)

  set(REDSCRIPT_MODULE_IN_FILE "${MOD_REDSCRIPT_DIR}/${REDSCRIPT_MODULE_IN_FILENAME}")

  if(EXISTS "${REDSCRIPT_MODULE_IN_FILE}")
    configure_file("${REDSCRIPT_MODULE_IN_FILE}" "${MOD_GAME_DIR_MODULE_FILE}" @ONLY)
    add_custom_target(${MOD_SLUG}_redscript_module
      DEPENDS ${MOD_GAME_DIR_MODULE_FILE}
      SOURCES ${REDSCRIPT_MODULE_IN_FILE})
    set_target_properties(${MOD_SLUG}_redscript_module PROPERTIES FOLDER "${FOLDER_PREFIX}Redscript")
    add_dependencies(${MOD_SLUG} ${MOD_SLUG}_redscript_module)

    list(APPEND ${MOD_PREFIX}_GAME_DIR_FILES ${MOD_GAME_DIR_MODULE_FILE})
  endif()

  foreach(REDSCRIPT_DEPENDENCY_FILE ${REDSCRIPT_DEPENDENCIES_SOURCE_FILES})
    file(RELATIVE_PATH REL_PATH ${PROJECT_SOURCE_DIR} ${REDSCRIPT_DEPENDENCY_FILE})
    configure_file("${REDSCRIPT_DEPENDENCY_FILE}" "${REDSCRIPT_PRECOMPILE_DIR}/${REL_PATH}")
  endforeach()

  add_custom_command(
    OUTPUT ${REDSCRIPT_PREREQ_FILE}
    DEPENDS ${REDSCRIPT_DEPENDENCIES_SOURCE_FILES}
    COMMAND ${REDSCRIPT_CLI_EXE}
    ARGS compile -s "${REDSCRIPT_PRECOMPILE_DIR}" -b "${CYBERPUNK_2077_REDSCRIPT_BACKUP}" -o "${REDSCRIPT_PREREQ_FILE}"
    COMMENT "Precompiling redscript dependencies")

  file(GLOB_RECURSE REDSCRIPT_SOURCE_FILES ${MOD_REDSCRIPT_DIR}/*.reds LIST_DIRECTORIES false)

  list(APPEND CMAKE_MESSAGE_INDENT "  ")

  foreach(FILE ${REDSCRIPT_SOURCE_FILES})
    file(RELATIVE_PATH RELATIVE_PATH "${MOD_REDSCRIPT_DIR}" "${FILE}")
    message(STATUS "Found ${RELATIVE_PATH}")
  endforeach()

  list(POP_BACK CMAKE_MESSAGE_INDENT)

  if(NOT PROJECT_IS_TOP_LEVEL)
    list(APPEND REDSCRIPT_DEPENDENCIES_SOURCE_FILES ${REDSCRIPT_SOURCE_FILES})
    set(REDSCRIPT_DEPENDENCIES_SOURCE_FILES ${REDSCRIPT_DEPENDENCIES_SOURCE_FILES} CACHE INTERNAL "Files used in compiling redscript dependencies")
  endif()

  if(NOT ${CMAKE_CI_BUILD})
    add_custom_command(
      OUTPUT ${REDSCRIPT_LAST_LINT}
      DEPENDS ${REDSCRIPT_PREREQ_FILE} ${REDSCRIPT_SOURCE_FILES}
      COMMAND ${REDSCRIPT_CLI_EXE} lint -s ${MOD_REDSCRIPT_DIR} -b ${REDSCRIPT_PREREQ_FILE} && echo "1" > ${REDSCRIPT_LAST_LINT}
      COMMENT "Linting redscript against pre-compiled prereqs"
      USES_TERMINAL)

    add_custom_target(${MOD_SLUG}_redscript_lint
      DEPENDS ${REDSCRIPT_PREREQ_FILE}
      COMMAND ${REDSCRIPT_CLI_EXE} lint -s ${MOD_REDSCRIPT_DIR} -b ${REDSCRIPT_PREREQ_FILE} && echo "1" > ${REDSCRIPT_LAST_LINT}
      USES_TERMINAL)
    set_target_properties(${MOD_SLUG}_redscript_lint PROPERTIES FOLDER "${FOLDER_PREFIX}Redscript")
  else()
    add_custom_command(
      OUTPUT ${REDSCRIPT_LAST_LINT}
      COMMAND echo "1" > ${REDSCRIPT_LAST_LINT}
      USES_TERMINAL)
    add_custom_target(${MOD_SLUG}_redscript_lint
      COMMAND echo "1" > ${REDSCRIPT_LAST_LINT}
      USES_TERMINAL)
  endif()

  add_custom_command(
    OUTPUT ${MOD_GAME_DIR_PACKED_FILE}
    BYPRODUCTS ${MOD_GAME_DIR_PACKED_FILE}
    DEPENDS ${MOD_SLUG}_redscript_lint ${REDSCRIPT_SOURCE_FILES}
    COMMAND ${CMAKE_COMMAND} -D COMMENT_SLUG="//" -D GLOB_EXT="reds" -D HEADER_FILE="${MOD_HEADER_TXT_FILE}" -D PACKED_FILE=${MOD_GAME_DIR_PACKED_FILE} -D SEARCH_FOLDER=${MOD_REDSCRIPT_DIR} -P ${CYBERPUNK_CMAKE_SCRIPTS}/PackFiles.cmake
    COMMENT "Packing redscript files into one")

  add_custom_target(${MOD_SLUG}_redscript
    DEPENDS ${REDSCRIPT_LAST_LINT} ${MOD_GAME_DIR_PACKED_FILE}
    SOURCES ${REDSCRIPT_SOURCE_FILES})
  set_target_properties(${MOD_SLUG}_redscript PROPERTIES FOLDER "${FOLDER_PREFIX}Redscript")
  add_dependencies(${MOD_SLUG} ${MOD_SLUG}_redscript)

  list(APPEND ${MOD_PREFIX}_GAME_DIR_FILES ${MOD_GAME_DIR_PACKED_FILE})
  if(NOT ${REDSCRIPT_PACK_INTO_RED4EXT})
    list(APPEND ${MOD_PREFIX}_GAME_DIR_FOLDERS ${MOD_GAME_DIR_REDSCRIPT_DIR}/${MOD_SLUG})
  endif()
endmacro()
